plugins {
    id "java"
    id "net.minecraftforge.licenser" version "1.0.1"
    id "me.champeau.jmh" version "0.7.2"
    id "java-library"
    id "maven-publish"
}

group = property("group") + ".konvo"
version = property("version")

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "paper-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT"

    api project(":konvo")

    testImplementation platform("org.junit:junit-bom:5.11.0")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "com.github.seeseemelk:MockBukkit-v1.19:3.1.0"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    jmh "org.openjdk.jmh:jmh-core:1.37"
    jmh "org.openjdk.jmh:jmh-generator-annprocess:1.37"
    jmhAnnotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:1.37"
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = this.group
            artifactId = 'konvo-spigot'
            version = this.version

            from components.java
        }
    }
}

tasks {
    assemble {
        dependsOn("updateLicenses")
    }
}

jmh {
    benchmarkMode = ["avgt"]
    timeUnit = "ns"
}

license {
    header = project.file("$rootDir/HEADER")
    properties {
        name = "Kaspian"
        year = 2024
    }

    include "**/*.java"
}
