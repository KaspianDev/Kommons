plugins {
    id "java"
    id "com.gradleup.shadow" version "8.3.0"
    id "net.minecraftforge.licenser" version "1.0.1"
    id "maven-publish"
}

group = property("group") + ".konfig"
version = property("version")

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.junit:junit-bom:5.11.0")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    implementation "org.yaml:snakeyaml:2.2"
}

test {
    useJUnitPlatform()
}

jar.enabled = false

shadowJar {
    setArchiveClassifier(null)

    relocate "org.yaml.snakeyaml", this.group + ".snakeyaml"
}

publishing {
    publications {
        maven(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

tasks {
    assemble {
        dependsOn("updateLicenses")
    }
    build {
        dependsOn("shadowJar")
    }
}

license {
    header = project.file("$rootDir/HEADER")
    properties {
        name = "Kaspian"
        year = 2024
    }

    include "**/*.java"
}
